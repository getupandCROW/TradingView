// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© RicardoSantos, LucF

//@version=4
study(title="Highest/Lowest Functions", overlay=true)

int length01  = input(defval=10, title="Length A:")
int length02  = input(defval=20, title="Length B:")
float srcHi = input(defval=high, title="Source for high series:")
float srcLo = input(defval=low, title="Source for low series:")

int alternating_length = close > open ? length01 : length02

//use this for long length series under 5000.
f_highest(_source, _length) =>
    float _return = _source
    if _length >= 1 and _length <= 5000
        for _i = 0 to max(0, _length-1)
            _return := max(_source[_i], _return)
    _return

f_lowest(_source, _length) => 
    float _return = _source
    if _length >= 1 and _length <= 5000
        for _i = 0 to max(0, _length-1)
            _return := min(_source[_i], _return)
    _return

// use this for short length series, it will error if supplied a long length.
// will map series starting at bar 0.
// bar_index causes issues with high length counts.
f_highest_sr(_source, _length) =>
    float _return = _source
    for _i = 0 to max(0, min(_length, bar_index)-1)
        _return := max(_source[_i], _return)
    _return

f_lowest_sr(_source, _length) => 
    float _return = _source
    for _i = 0 to max(0, min(_length, bar_index)-1)
        _return := min(_source[_i], _return)
    _return
    

float high_with_alt_length = f_highest(srcHi, alternating_length)
float low_with_alt_length = f_lowest(srcLo, alternating_length)
plot(series=high_with_alt_length, title="Hi Series Length", color=color.gray, linewidth=2, style=plot.style_circles, transp = 0)
plot(series=low_with_alt_length, title="Lo Series Length", color=color.gray, linewidth=2, style=plot.style_circles, transp = 0)

plot(series=f_highest(srcHi, length01), title="Hi custom", color=color.black, linewidth=1, transp = 0)
plot(series=f_lowest(srcLo, length01), title="Lo custom", color=color.black, linewidth=1, transp = 0)
plot(series=highest(srcHi, length01), title="Hi built-in", color=color.orange, linewidth=3, transp = 55)
plot(series=lowest(srcLo, length01), title="Lo built-in", color=color.orange, linewidth=3, transp = 55)
